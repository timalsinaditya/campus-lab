ORG 0000H

NUM EQU 24
FIRSTROLL EQU 24H

MOV DPTR, #400H
MOV 40H, #0FFH
MOV 41H, #0FFH
MOV 42H, #0FFH
MOV 43H, #0FFH
MOV R4, #0
MOV R5, #NUM
MOV B,  #FIRSTROLL


START:	MOV A, R4
		CLR C
		SUBB A, #1
		JC FIRSTCHAR ;FIRSTDIGIT
		JZ SECONDCHAR ;SECONDDIGIT
		SJMP THIRDCHAR ;DOT


FIRSTCHAR:	MOV A,B
			SWAP A
			ANL A, #0FH
			SJMP UPDATE

SECONDCHAR: MOV A, B
			ANL A, #0FH
			SJMP UPDATE

THIRDCHAR:	MOV A, B
			ADD A, #1
			DA A
			MOV B, A
			DJNZ R5, SKIP
			SJMP RESTART
			SKIP: MOV A, #10
			SJMP UPDATE

RESTART:	MOV R5, #NUM
			MOV B, #FIRSTROLL
			SJMP SKIP

UPDATE: 	MOVC A, @A+DPTR
			MOV 43H, A
			INC R4
			MOV A, R4
			CLR C
			SUBB A, #3
			JC DISP
			MOV R4, #0

DISP:		MOV R6, #120 ;SHOWS THE OUTPUT 20 TIMES
AGAIN:		MOV P0, 40H
			MOV P2, #01H
			ACALL DELAYS
			MOV P2, #00H
			MOV P0, 41H
			MOV P2, #02H
			ACALL DELAYS
			MOV P2, #00H
			MOV P0, 42H
			MOV P2, #04H
			ACALL DELAYS
			MOV P2, #00H
			MOV P0, 43H
			MOV P2, #08H
			ACALL DELAYS
			MOV P2, #00H
			DJNZ R6, AGAIN

SCROLL:		MOV 40H, 41H
			MOV 41H, 42H
			MOV 42H, 43H
			SJMP START
	
DELAYS: MOV 31H, #255
HERE1: DJNZ 31H, HERE1
RET

ORG 0400H
DB 0C0H, 0F9H, 0A4H, 0B0H, 99H, 92H, 82H, 0F8H, 80H, 98H, 07FH
END



